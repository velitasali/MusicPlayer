apply plugin: 'com.android.application'

repositories {
    maven {
        name 'glide-snapshot'
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27

        renderscriptTargetApi 25
        vectorDrawables.useSupportLibrary = true

        applicationId "code.name.monkey.retromusic"
        versionCode 104
        versionName 'R - 1.1.120'

        multiDexEnabled true

        buildConfigField("String", "LASTFM_API_KEY", "\"${getProperty(getProperties('../public.properties'), 'LASTFM_API_KEY')}\"")
        buildConfigField("String", "GOOGLE_PLAY_LICENSE_KEY", "\"${getProperty(getProperties('../public.properties'), 'GOOGLE_PLAY_LICENSE_KEY')}\"")

        resConfigs "en", "de", "es", "es-rES", "es-rUS", "fr", "hu", "id", "in", "it", "ja", "pt-rBR", "pl", "ru", "tr", "zh-rCN", "zh-rHK", "zh-rTW"

    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'

            ext.enableCrashlytics = false // Disable fabric build ID generation for debug builds
        }
    }
    flavorDimensions "default"
    productFlavors {
        normal {
            versionCode defaultConfig.versionCode + 10000
            versionName defaultConfig.versionName + " BETA 8"
            dimension "default"
        }
        kitkat {
            minSdkVersion 19
            targetSdkVersion 19
            versionCode defaultConfig.versionCode + 2000
            versionName defaultConfig.versionName + " Beta 1"
            dimension "default"
        }
        ios {
            dimension "default"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '27.0.2'
                }
            }
        }
    }
}

@Nullable
def getProperties(String fileName) {
    final def Properties properties = new Properties()
    def file = file(fileName)
    if (file.exists()) {
        file.withInputStream { stream -> properties.load(stream) }
    }
    return properties
}

def getProperty(@Nullable Properties properties, String name) {
    return properties.getProperty(name) ?: "$name missing";
}

ext {
    supportLibVersion = "27.0.2"
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:multidex:1.0.2'
    implementation "com.android.support:support-compat:$supportLibVersion"
    implementation "com.android.support:support-v13:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:gridlayout-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "com.android.support:preference-v7:$supportLibVersion"
    implementation "com.android.support:preference-v14:$supportLibVersion"
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.afollestad:material-cab:0.1.12'
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.5.0'
    implementation 'com.github.kabouzeid:app-theme-helper:1.3.7'
    implementation 'com.afollestad.material-dialogs:core:0.9.4.5@aar'
    implementation 'com.afollestad.material-dialogs:commons:0.9.4.5@aar'
    implementation 'com.github.kabouzeid:RecyclerView-FastScroll:1.0.16-kmod'
    implementation 'com.github.kabouzeid:AndroidSlidingUpPanel:3.3.0-kmod3'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.1'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.2'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.github.stfalcon:multiimageview:0.1'
    implementation('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar') {
        transitive = true
    }
    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    implementation 'com.andkulikov:transitionseverywhere:1.7.6'
    implementation project(':backend')
    implementation 'com.anjlab.android.iab.v3:library:1.0.44'
    implementation 'org.jsoup:jsoup:1.10.3'
    implementation 'com.lucasurbas:listitemview:1.0.5'
    implementation 'jp.wasabeef:glide-transformations:2.0.2'
    implementation 'com.github.jetradarmobile:android-snowfall:1.1.6'
}