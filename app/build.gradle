apply plugin: 'com.android.application'

repositories {
    maven {
        name 'glide-snapshot'
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27

        renderscriptTargetApi 27 //must match target sdk and build tools
        vectorDrawables.useSupportLibrary = true

        applicationId "code.name.monkey.retromusic"
        versionCode 112
        versionName 'R - 1.1.465'

        multiDexEnabled true

        buildConfigField("String", "LASTFM_API_KEY", "\"${getProperty(getProperties('../public.properties'), 'LASTFM_API_KEY')}\"")
        buildConfigField("String", "GOOGLE_PLAY_LICENSE_KEY", "\"${getProperty(getProperties('../public.properties'), 'GOOGLE_PLAY_LICENSE_KEY')}\"")

        resConfigs "en", "de", "es", "es-rES", "es-rUS", "fr", "hu", "id", "in", "it", "ja", "pt-rBR", "pl", "ru", "tr", "zh-rCN", "zh-rHK", "zh-rTW"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'

            ext.enableCrashlytics = false // Disable fabric build ID generation for debug builds
        }
    }
    flavorDimensions "default"
    productFlavors {
        normal {
            versionCode defaultConfig.versionCode + 10000
            versionName defaultConfig.versionName
            dimension "default"
        }
        kitkat {
            minSdkVersion 19
            targetSdkVersion 19
            versionCode defaultConfig.versionCode + 2000
            versionName defaultConfig.versionName
            dimension "default"
        }
        pro {
            versionName defaultConfig.versionName
            applicationId defaultConfig.applicationId + ".pro"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '27.0.2'
                }
            }
        }
    }
}

@Nullable
def getProperties(String fileName) {
    final def Properties properties = new Properties()
    def file = file(fileName)
    if (file.exists()) {
        file.withInputStream { stream -> properties.load(stream) }
    }
    return properties
}

def getProperty(@Nullable Properties properties, String name) {
    return properties.getProperty(name) ?: "$name missing";
}

ext {
    supportLibVersion = "27.0.2"
    firebase = "11.8.0"
    retrofit = "2.3.0"
    butterKnife = "8.8.1"
    materialDialog = "0.9.4.5@aar"
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:multidex:1.0.2'
    implementation "com.android.support:support-compat:$supportLibVersion"
    implementation "com.android.support:support-v13:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:gridlayout-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "com.android.support:preference-v7:$supportLibVersion"
    implementation "com.android.support:preference-v14:$supportLibVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit"
    implementation "com.jakewharton:butterknife:$butterKnife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnife"
    implementation "com.afollestad.material-dialogs:core:$materialDialog"
    implementation "com.afollestad.material-dialogs:commons:$materialDialog"
    implementation 'com.afollestad:material-cab:0.1.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.2'
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.5.0'
    implementation 'com.github.kabouzeid:AndroidSlidingUpPanel:3.3.0-kmod3'
    implementation('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar') {
        transitive = true
    }
    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    implementation 'com.github.kabouzeid:RecyclerView-FastScroll:1.0.16-kmod'
    implementation 'com.anjlab.android.iab.v3:library:1.0.44'
    implementation 'jp.wasabeef:glide-transformations:2.0.2'
    /*UI Library*/
    implementation 'com.github.jetradarmobile:android-snowfall:1.1.6'
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.1'
    /*Backend all*/
    implementation project(':back-end')
    implementation project(':appthemehelper')
}
apply plugin: 'com.google.gms.google-services'